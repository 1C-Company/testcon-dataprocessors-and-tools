#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрошлыйГод = ДобавитьМесяц(ТекущаяДата(), -12);
	ДатаНачала = НачалоГода(ПрошлыйГод);
	ДатаКонца  = КонецГода(ПрошлыйГод);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сдвинуть(Команда)
	
	ВыполнитьНаСервере(Организация, ДатаНачала, ДатаКонца, КоличествоМесяцев, Ложь);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Скопировать(Команда)
	
	ВыполнитьНаСервере(Организация, ДатаНачала, ДатаКонца, КоличествоМесяцев, Истина);
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ВыполнитьНаСервере(Организация, ДатаНачала, ДатаКонца, КоличествоМесяцев, Копировать)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаНачала",  НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаКонца",   КонецДня(ДатаКонца));
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЖурналОпераций.Дата,
	|	ЖурналОпераций.Ссылка
	|ИЗ
	|	ЖурналДокументов.ЖурналОпераций КАК ЖурналОпераций
	|ГДЕ
	|	ЖурналОпераций.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И НЕ ЖурналОпераций.Ссылка ССЫЛКА Документ.ВводНачальныхОстатков
	|	И НЕ ЖурналОпераций.Ссылка ССЫЛКА Документ.РегламентнаяОперация
	|	И &УсловиеПоОрганизации
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЖурналОпераций.Дата,
	|	ЖурналОпераций.Ссылка";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизации", "ЖурналОпераций.Организация = &Организация");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизации", "ИСТИНА");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Копировать Тогда
			Объект = Выборка.Ссылка.Скопировать();
		Иначе
			Объект = Выборка.Ссылка.ПолучитьОбъект();
		КонецЕсли;
		
		Объект.УстановитьНовыйНомер();
		Объект.Дата = ДобавитьМесяц(Выборка.Дата, КоличествоМесяцев);
		
		Объект.ОбменДанными.Загрузка = Истина;
		Объект.Записать(РежимЗаписиДокумента.Запись);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти