#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ФормированиеТестовыхДанных_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ПравоДоступаРедактированиеБанковскогоСчета = ПравоДоступа("Редактирование", Метаданные.Справочники.БанковскиеСчета);
	Элементы.ВалютаДенежныхСредствРасширение.Доступность = ПравоДоступаРедактированиеБанковскогоСчета;
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	РедактироватьВалютуСчета = ЯвляетсяБанкомРФ;
	
	ЗаполнитьСлучайнымиРеквизитамиРасчетногоСчетаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ФормированиеТестовыхДанных_ПриОткрытииПосле(Отказ)
	
	Элементы.ВалютаДенежныхСредствРасширение.Видимость = РедактироватьВалютуСчета;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФормированиеТестовыхДанных_БанкПриИзменении(Элемент)
	
	ЗаполнитьСлучайнымиРеквизитамиРасчетногоСчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ФормированиеТестовыхДанных_БанкОчистка(Элемент, СтандартнаяОбработка)
	
	Объект.Банк = Неопределено;
	Объект.НомерСчета = Неопределено;
	БИКБанка = "";
	
	БанковскиеСчетаФормыКлиент.УстановитьВалютуПодсказкуСчета(
		Объект, ЭтотОбъект, БИКБанка, ЦветВыделенияНекорректногоЗначение, ЯвляетсяБанкомРФ);
	
	ПодсказкаБанк = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляБанка(ДеятельностьБанкаПрекращена);
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ФормированиеТестовыхДанных_ВалютаДенежныхСредствРасширениеПриИзменении(Элемент)
	
	ЗаполнитьСлучайнымиРеквизитамиРасчетногоСчета();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьСлучайнымиРеквизитамиРасчетногоСчета()
	
	ЗаполнитьСлучайнымиРеквизитамиРасчетногоСчетаНаСервере();
	
	БанковскиеСчетаФормыКлиент.УстановитьВалютуПодсказкуСчета(
		Объект, ЭтотОбъект, БИКБанка, ЦветВыделенияНекорректногоЗначение, ЯвляетсяБанкомРФ);
	
	ПодсказкаБанк = БанковскиеСчетаФормыКлиентСервер.ПодсказкаПоляБанка(ДеятельностьБанкаПрекращена);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлучайнымиРеквизитамиРасчетногоСчетаНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.Владелец) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРасчетногоСчета = ФормированиеТестовыхДанных.СлучайныеРеквизитыРасчетногоСчета(
		БИКБанка,
		Объект.ВалютаДенежныхСредств,
		ЮридическоеФизическоеЛицо(),
		ЭтоГосОрган());
		
	Если СтруктураРасчетногоСчета = Неопределено ИЛИ НЕ ЗначениеЗаполнено(СтруктураРасчетногоСчета.НомерСчета) Тогда
		РедактироватьВалютуСчета = Ложь;
		Возврат;
	КонецЕсли;
	
	РедактироватьВалютуСчета = СтруктураРасчетногоСчета.ЯвляетсяБанкомРФ;
	ЗаполнитьЗначенияСвойств(Объект, СтруктураРасчетногоСчета);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураРасчетногоСчета);
	
КонецПроцедуры

&НаСервере
Функция ЮридическоеФизическоеЛицо()
	
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Возврат Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	Иначе
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ЮридическоеФизическоеЛицо");
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ЭтоГосОрган()
	
	ЭтоГосОрган = Ложь;
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Контрагенты") Тогда
		ЭтоГосОрган = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ГосударственныйОрган");
	КонецЕсли;
	
	Возврат ЭтоГосОрган;
	
КонецФункции

#КонецОбласти